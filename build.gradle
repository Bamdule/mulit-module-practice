buildscript {
    ext {
        springBootVersion = '3.0.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'io.spring'

    sourceCompatibility = '17'
    targetCompatibility = '17'
    //Java 소스 코드 UTF-8로 인코딩 (한글이 깨지는 것을 방지)
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }
}

project(':core-api') { // 컴파일 시 multi-module-database 로딩
    bootJar { enabled = true }
    jar { enabled = false }
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation project(':core-domain')
        runtimeOnly project(':storage:jpa-storage')
    }
}

project(':core-domain') {
    bootJar { enabled = false }
    jar { enabled = true }
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    }
}

project(':storage:jpa-storage') {
    bootJar { enabled = false }
    jar { enabled = true }
    dependencies {
        compileOnly project(':core-domain')
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'com.h2database:h2'
    }
}